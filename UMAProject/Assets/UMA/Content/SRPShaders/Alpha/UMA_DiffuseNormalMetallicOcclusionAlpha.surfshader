BEGIN_OPTIONS
   ShaderName "UMA/Diffuse_Normal_Metallic_Occlusion_Alpha"
   Stackable "False"
   DisableGBufferPass "True" // disable gbuffer pass
   Workflow "Metallic"
   Queue "Transparent"
   Alpha "Blend"
END_OPTIONS

BEGIN_PROPERTIES

    [BetterHeader(Material Specific)]
	_Color ("Main Color", Color) = (1, 1, 1, 1)
	_ColorModulation ("Color Modulation", Range(0,2)) = 1
	_MetallicModulation ("Metallic Level", Range(0,2)) = 1
	_SmoothnessModulation ("Smoothness Level", Range(0,2)) = 1
	[BetterHeader(Lighting Adjustment)]
    [Toggle]_UseHalfLambert ("Use Half Lambert", Float) = 1
    _HalfLambertStrength ("Lambert(0), HalfLambert(1)", Range(0,4)) = 1
    _RimLightingModulation ("Rim Lighting Level", Float) = 0
	_RimLightColor("Rim Light Color", Color) = (1,1,1,1)
	_RimLightPower("Rim Light Power", Range(0.5, 12)) = 4
    _AlphaModulation("Alpha Modulation", Range(0,2)) = 1
    [Enum(Off,0,Front,1,Back,2)]_CullMode("Culling Mode", Float) = 2
    [Enum(Flip, 0, Mirror, 1, None, 2)] _DoubleSidedNormalMode("Double sided normal mode", Float) = 1

    [BetterHeader(Textures)]
    [NoScaleOffset]_BaseMap("BaseMap", 2D) = "white" {}
    [NoScaleOffset]_BumpMap("_BumpMap", 2D) = "bump" {}
    _NormalStrength ("Normal Strength", Range(0,2)) = 1
    [NoScaleOffset]_MetallicGlossMap("_MetallicGlossMap", 2D) = "black" {}
    [NoScaleOffset]_OcclusionMap("OcclusionMap", 2D) = "white" {}
END_PROPERTIES

BEGIN_PASS("All")
   Cull [_CullMode]
END_PASS

// Any variables you want to have in the per material CBuffer go here.
BEGIN_CBUFFER
    half4 _Color;
	half  _ColorModulation;
	half  _SmoothnessModulation;
	half  _MetallicModulation;
    half  _EmissiveModulation;
    half _HalfLambertStrength;
    float _DoubleSidedNormalMode;
    half3 _RimLightColor;
	half  _RimLightPower;
    float _RimLightingModulation;
    float _NormalStrength;
    half _AlphaModulation;
    half _UseHalfLambert;
END_CBUFFER


// All code goes here
BEGIN_CODE

   TEXTURE2D(_BaseMap);
   SAMPLER(sampler_BaseMap);
   TEXTURE2D(_BumpMap);
   TEXTURE2D(_MetallicGlossMap);
   TEXTURE2D(_OcclusionMap);


	void SurfaceFunction(inout Surface o, ShaderData d)
	{
      float2 auv = d.texcoord0.xy;
      float2 nuv = d.texcoord0.xy;
      float2 cuv = d.texcoord0.xy;

	  half4 mask = SAMPLE_TEXTURE2D(_MetallicGlossMap,sampler_BaseMap,cuv);
	  half4 base_c = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, auv);
	  half4 basepackednormal = SAMPLE_TEXTURE2D(_BumpMap, sampler_BaseMap,nuv);
	  half3 base_n = UnpackScaleNormal(basepackednormal,_NormalStrength);
      half4 base_occlusion = SAMPLE_TEXTURE2D(_OcclusionMap, sampler_BaseMap, auv);

	  half base_smooth = mask.a;
	  half base_Metallic = mask.r;

      o.Alpha = base_c.a * _AlphaModulation * _Color.a;
	  
      o.Albedo = base_c * _ColorModulation * _Color.rgb;
	  o.Normal = base_n;
	  o.Metallic = base_Metallic * _MetallicModulation;
	  o.Smoothness = base_smooth * _SmoothnessModulation;
      o.Occlusion = base_occlusion.r;

      if (_DoubleSidedNormalMode == 0)
      {
         o.Normal *= d.isFrontFace ? 1 : -1;
      }
      else if (_DoubleSidedNormalMode == 1)
      {
		 o.Normal.z *= d.isFrontFace ? 1 : -1;
      }

      if (_UseHalfLambert)
      {
	     float3 lightDir;
         float3 lightColor;
	     GetSun(lightDir, lightColor);
	     float3 normalDirection = TangentToWorldSpace(d, o.Normal);
	     float atten = 1.0;
	     float lambert = dot(normalDirection, lightDir);

         lambert = lerp(lambert, lambert * 0.5 + 0.5, _HalfLambertStrength);
         o.Albedo *= atten * lightColor * lambert;
      }

     if (_RimLightingModulation>0) 
     { 
	    float rim = 1.0 - saturate(dot(d.tangentSpaceViewDir, o.Normal)); 
	    o.Emission += (pow(rim, _RimLightPower) * _RimLightColor) * _RimLightingModulation;  
      }
	}

END_CODE

